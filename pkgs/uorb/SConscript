import os
import subprocess
import sys
import rtconfig
from building import *

cwd = GetCurrentDir()


def gen_uorb_msgs():
    # msg文件所在路径
    msg_path = os.path.abspath(os.path.join(cwd, "../../msgs/uorb_msgs"))
    # 获取所有msg文件
    msg_files = Glob(os.path.join(cwd, "../../msgs/uorb_msgs/*.msg"), strings=True)
    msg_files = [os.path.relpath(x, msg_path) for x in msg_files]

    # 需要执行的命令
    cmd = [
        sys.executable,
        os.path.abspath(os.path.join(cwd, "tools/generate_uorb_topic_files.py")),
        "--header",  # 生成header或source
        "-i",
        msg_path,
        "-o",  # 代码生成目录
        os.path.abspath(os.path.join(cwd, "topics")),
        "-e",  # 模板所在目录
        os.path.abspath(os.path.join(cwd, "tools/templates/uorb")),
        "-f",  # msg文件
    ] + msg_files

    # 生成header文件
    cmd[2] = "--headers"
    subprocess.call(cmd, cwd=msg_path)
    # 生成source文件
    cmd[2] = "--sources"
    subprocess.call(cmd, cwd=msg_path)


objs = []

if GetDepend(["PKG_USING_UORB"]):

    gen_uorb_msgs()

    src = Glob("*.cpp") + Glob("topics/*.cpp")
    inc = [cwd]

    objs += DefineGroup("pkg/uorb", src, depend=[""], CPPPATH=inc)

    for d in os.listdir(cwd):
        path = os.path.join(cwd, d)
        if os.path.isfile(os.path.join(path, "SConscript")):
            objs += SConscript(os.path.join(d, "SConscript"))
else:
    objs = []

Return("objs")
